* CHARMM input file for Multi-Site lambda-dynamics
* MSLD references:
*    J.L. Knight and C.L. Brooks III. Multi-site lambda-dynamics: 
*       free energy method for simulating Structure-Activity Relationship 
*       studies. J. Chem. Theory Comput. 7, 2728-2739 (2011). 
*    J.L. Knight and C.L. Brooks III. Applying efficient 
*       implicit non-geometric constraints in free energy simulations.
*       J. Comput. Chem. 32, 3423-3432 (2011). 
* Stream file for MSLD created by process.py
* Written by Ryan Hayes 2016-06-18
*

trim sysname from 2

set fnex = 5.5
set temp = 298.15

bomblev -1

set nnodes = 1
stream "scratch2/@{SYSNAME}0.inp"

! Set nonbonded options
stream prep/nbond.str

faster on

! Make sure all atoms have coordinates
PRINt COORdinate SELEct .NOT. INITialized END

! cons fix selection ( .not. hydrogen ) .and. ( .not. type OT2 ) .and. ( segid PROT ) show end
cons fix selection ( .not. hydrogen ) .and. ( .not. type OT2 ) .and. ( segid PR* ) show end
mini sd nstep 100 nprint 10 step 0.005                  !! Do min, Steepest-Decent
cons fix selection none end 
! 1000 kcal/mol/nm^2 - use position restraints because constraints don't work with shake fast
! cons harm abso force 10.0 selection ( .not. hydrogen ) .and. ( .not. type OT2 ) .and. ( segid PROT ) show end main
cons harm abso force 10.0 selection ( .not. hydrogen ) .and. ( .not. type OT2 ) .and. ( segid PR* ) show end main
mini sd nstep 100 nprint 10 step 0.005                  !! Do min, Steepest-Decent

!---------------------------------------------------------------------------
! Write out structure after minimization
!---------------------------------------------------------------------------

system "mkdir scratch3"

write psf card name scratch3/aftermin.psf 
* after minimization psf
*
! write coor pdb name scratch3/minimized.pdb 
! * after minimization pdb
! *
write coor card name scratch3/minimized.crd 
* after minimization crd
*
! read coor pdb resid name prep/minimized.pdb

! show buil
echo ?xtla

!---------------------------------------------------------------------------
! Setup of dynamics run
!---------------------------------------------------------------------------

shake fast bonh param
scalar fbeta set 1.0 sele all end

! domdec gpu only dlb off ndir 1 @nnodes 1
blade on

!! Heating run of 100 ps
open unit 21 write unform name "scratch3/heat.dcd"
open unit 22 write form name "scratch3/heat.res"
! open unit 24 write file name "res/@{SYSNAME}_1_heat.lmd"

set resttoken = start
set iurest = -1

! calc seed = @seed + @myrep
calc pmass = ?natom * 0.12
dynamics cpt @resttoken -
  - ! iseed @seed @seed @seed @seed -
  blade prmc iprs 100 pref 1 prdv 100 -
  timestep 0.002 -      !!timestep interval
  nstep 50000 - ! 2500000 -         !!no. of steps  (== 200 ps)
  nprint 1000 -         !!freq of printout
  iprfrq 1000 -         !!freq of calc avg/rms energy
  nsavc 1000 -          !!freq of writing coordinates
  nsavl 10 -            !!freq of writing lambda histograms
  isvfrq 10000 -        !!freq of writing out restart files
  iunread @iurest -          !!read restart file
  iuncrd 21 -           !!write out coordinates
  iunwri 22 -           !!write out restart file
  - ! iunldm 24 -           !!write out lambda histograms and biasing potential
  firstt @temp -          !!initial temp
  finalt @temp -          !!final temp
  tstruc @temp -          !!temp at which starting structure has been equilibrated
  tbath @temp -           !!temp of langevin bath
  pconstant -           !! use constant pressure (pressure.doc recommendations)
  pmass @pmass -         !! pressure "mass" 2% of mass or 400.0 amu
  pref 1.0 -            !! pressure in atm
  pgamma 20.0 -         !! pressure collision frequency
  hoover -
  reft @temp -
  tmass 1000 -
  ichecw 0 -            !!do not scale velocities to final temp (i.e. equilibrate)
  ihtfrq 0 -            !!freq of heating
  ieqfrq 0 -            !!freq of scaling/assigning velocity(X)
  iasors 1 -            !!assign velocities during heating (0 will be scale velocities)
  iasvel 1 -            !!using gaussian distribution to assign velocities
  iscvel 0 -
  inbfrq -1 -
  ilbfrq 0 -
  imgfrq -1 -
  ntrfrq 0 - ! 500 -
  echeck -1             !!energy tolerance check before crashing

echo ?xtla

!----------------------------------------------------------------------------
! Write out structure after simulation
!----------------------------------------------------------------------------

write psf card name "scratch3/annealed.psf"
* after dynamics psf
*
! write coor pdb name "scratch3/annealed.pdb"
! * after dynamics pdb
! *
write coor card name "scratch3/annealed.crd"
* after dynamics crd
*

! show buil
open unit 40 write card name scratch3/box.dat
echu 40
echo ?XTLA

stop

